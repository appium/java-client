import org.apache.tools.ant.filters.*

plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
    id 'org.owasp.dependencycheck' version '8.4.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext {
    seleniumVersion = project.property('selenium.version')
    appiumClientVersion = project.property('appiumClient.version')
    slf4jVersion = '2.0.7'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    api ('org.seleniumhq.selenium:selenium-api') {
        version {
            strictly "[${seleniumVersion}, 5.0)"
            prefer "${seleniumVersion}"
        }
    }
    api ('org.seleniumhq.selenium:selenium-remote-driver') {
        version {
            strictly "[${seleniumVersion}, 5.0)"
            prefer "${seleniumVersion}"
        }
    }
    implementation ('org.seleniumhq.selenium:selenium-support') {
        version {
            strictly "[${seleniumVersion}, 5.0)"
            prefer "${seleniumVersion}"
        }
    }
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'commons-io:commons-io:2.13.0'
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation (group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.3.3') {
        exclude group: 'org.seleniumhq.selenium'
    }
    testImplementation platform(group: 'org.seleniumhq.selenium', name: 'selenium-bom', version: '4.11.0')
    testImplementation 'org.seleniumhq.selenium:selenium-api'
    testImplementation 'org.seleniumhq.selenium:selenium-remote-driver'
    testImplementation 'org.seleniumhq.selenium:selenium-support'
    testImplementation 'org.seleniumhq.selenium:selenium-chrome-driver'
    testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
}

ext {
    Sources = fileTree("$buildDir/src/main/java").include('**/*.java')
    Tests = fileTree("$buildDir/src/test/java").include('**/*.java')
    Docs = file("$buildDir/doc")
}

dependencyCheck {
    failBuildOnCVSS=22
}

jacoco {
    toolVersion = '0.8.8'
}

tasks.withType(JacocoReport) {
    description = 'Generate Jacoco coverage reports after running tests'
    sourceSets sourceSets.main
    reports {
        html.required = true
        html.outputLocation = file("${buildDir}/Reports/jacoco")
    }
}
jacocoTestReport.dependsOn test

// Checkstyle requires Java 11 starting from 10.0
if (JavaVersion.current().isJava11Compatible()) {
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = '10.12.1'
        configFile = configDirectory.file('appium-style.xml').get().getAsFile()
        showViolations = true
        ignoreFailures = false
    }
}

javadoc {
    options.addStringOption('encoding', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.appium'
            artifactId = 'java-client'
            version = appiumClientVersion
            from components.java
            pom {
                name = 'java-client'
                description = 'Java client for Appium Mobile Webdriver'
                url = 'http://appium.io'
                developers {
                    developer {
                        name = 'Jonah Stiennon'
                        email = 'jonahss@gmail.com'
                        url = 'https://github.com/jonahss'
                        id = 'jonahss'
                    }
                    developer {
                        name = 'Sergey Tikhomirov'
                        email = 'tichomirovsergey@gmail.com'
                        url = 'https://github.com/TikhomirovSergey'
                        id = 'TikhomirovSergey'
                    }
                    developer {
                        name = 'Srinivasan Sekar'
                        email = 'srinivasan.sekar1990@gmail.com'
                        url = 'https://github.com/SrinivasanTarget'
                        id = 'SrinivasanTarget'
                    }
                    developer {
                        name = 'Mykola Mokhnach'
                        url = 'https://github.com/mykola-mokhnach'
                        id = 'mykola-mokhnach'
                    }
                    developer {
                        name = 'Valery Yatsynovich'
                        url = 'https://github.com/valfirst'
                        id = 'valfirst'
                    }
                }
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/appium/java-client'
                    connection = 'scm:git:ssh://git@github.com/appium/java-client.git'
                    developerConnection = 'scm:git:ssh://git@github.com/appium/java-client.git'
                    tag = 'HEAD'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/'"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    required { !'true'.equalsIgnoreCase(project.findProperty('signingDisabled')) }
    def signingKey = System.getenv("PGP_SECRET")
    def signingPassword = System.getenv("PGP_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

wrapper {
    gradleVersion = '8.3'
    distributionType = Wrapper.DistributionType.ALL
}

processResources {
    filter ReplaceTokens, tokens: [
            'selenium.version': seleniumVersion,
            'appiumClient.version': appiumClientVersion
    ]
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    filter {
        includeTestsMatching 'io.appium.java_client.drivers.options.*'
        includeTestsMatching 'io.appium.java_client.events.*'
        includeTestsMatching 'io.appium.java_client.internal.*'
        includeTestsMatching 'io.appium.java_client.proxy.*'
        includeTestsMatching 'io.appium.java_client.remote.*'
        includeTestsMatching 'io.appium.java_client.touch.*'
    }
    finalizedBy jacocoTestReport
}

task xcuiTest( type: Test ) {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    filter {
        includeTestsMatching 'io.appium.java_client.ios.*'
        includeTestsMatching '*.pagefactory_tests.XCUITModeTest'
        includeTestsMatching '*.pagefactory_tests.widget.tests.combined.*'
        includeTestsMatching '*.pagefactory_tests.widget.tests.ios.*'
        includeTestsMatching 'io.appium.java_client.service.local.StartingAppLocallyIosTest'
        exclude '**/IOSScreenRecordTest.class'
        exclude '**/ImagesComparisonTest.class'
    }
}

task uiAutomationTest( type: Test ) {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    filter {
        includeTestsMatching 'io.appium.java_client.android.SettingTest'
        includeTestsMatching 'io.appium.java_client.android.ClipboardTest'
        includeTestsMatching 'io.appium.java_client.android.OpenNotificationsTest'
        includeTestsMatching '*.AndroidAppStringsTest'
        includeTestsMatching '*.pagefactory_tests.widget.tests.android.*'
        includeTestsMatching '*.pagefactory_tests.widget.tests.AndroidPageObjectTest'
        includeTestsMatching 'io.appium.java_client.service.local.StartingAppLocallyAndroidTest'
        includeTestsMatching 'io.appium.java_client.service.local.ServerBuilderTest'
        includeTestsMatching 'io.appium.java_client.service.local.ThreadSafetyTest'
    }
}
